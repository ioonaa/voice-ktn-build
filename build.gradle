plugins {
    id "de.dfki.mary.voicebuilding-legacy" version "5.2.0"
    id "de.undercouch.download" version "3.2.0"
}

//apply plugin: 'java'

repositories {
    jcenter()
}

group 'de.uni-saarland.voicebuilding.group2'
version '0.1-SNAPSHOT'

//sourceCompatibility = 1.8

import de.undercouch.gradle.tasks.download.Download

def scriptDir = "$rootDir/scripts"
def dataURL = "https://github.com/ioonaa/voice-ktn-data/archive/master.zip"
def dataZip = "$buildDir/data.zip"
def flacURL = "https://github.com/ioonaa/voice-ktn-data/releases/download/0.1/voice-ktn.flac"
def flacPath = "$buildDir/ktn-voice.flac"
def yamlPath = "$buildDir/voice-ktn.yaml"

task downloadData(type: Download) {
    src dataURL
    dest dataZip
    overwrite false
}

task downloadFlac(type: Download) {
    src flacURL
    dest flacPath
    overwrite false
}

task extactYaml(type: Copy) {
    dependsOn downloadData
    from zipTree(dataZip), {
        include '**/*.yaml'
        eachFile {
            it.path = it.name
        }
    }
    into "$buildDir/"
}

task extractWav(type: Exec) {
    dependsOn downloadFlac, extactYaml
    /* write that Python must be installed */
    commandLine "python", file("$scriptDir/yaml_cut_wav_1616.py"), file(yamlPath), file(flacPath)
}

task extractTxt(type: Copy) {
    dependsOn downloadData
    from zipTree(dataZip), {
        include '**/text/*.txt'
        eachFile {
            it.path = it.name
        }
    }
    into "$buildDir/text"
}

task extractTG(type: Copy) {
    dependsOn downloadData
    from zipTree(dataZip), {
        include '**/tg/*.TextGrid'
        eachFile {
            it.path = it.name
        }
    }
    into "$buildDir/tg"
}

task makeLab(type: Exec) {
    dependsOn extractTG
    def script = file("$scriptDir/tg_to_xwaves.praat")
    commandLine "praat", "--run", script, "$buildDir/tg"
}

task moveLab(type: Copy) {
    from "$rootDir/scripts/", {
        include '**/*.lab'
    }
    into "$buildDir/lab/"
}

legacyInit {
    dependsOn extractWav, extractTxt, makeLab
}